#
# Test command script for the Monochron emulator
#
# Purpose: Test the top align type for fill types in glcdFillRectangle2()
#

# Variables defining the screen size, rectangle size, speed and align type (top)
vs h 127
vs v 63
vs sz 18
vs w 3
vs al 0

# Move rectangle top->right->bottom->left in the display
le

# Paint this in the center of the display
pa f 32 26 5x5p h 1 1 glcdfillrectangle2
pa f 42 32 5x5p h 1 1 - top align -

# Repeat for each fill type (1..3)
rw ft < 4 1 1
  # Move from top left to top right
  rw x <= (h-sz+1) 0 1
    prf f x 0 sz sz al ft
    w w
    pr b x 0 1 sz
  rn
  # Move from top right to bottom right
  rw y <= (v-sz+1) 0 1
    prf f (h-sz+1) y sz sz al ft
    w w
    pr b (h-sz+1) y sz 1
  rn
  # Move from bottom right to bottom left
  rw x >= 0 (h-sz) -1
    prf f x (v-sz+1) sz sz al ft
    pr b (x+sz) (v-sz+1) 1 sz
    w w
  rn
  # Move from bottom left to top left
  rw y >= 0 (v-sz) -1
    prf f 0 y sz sz al ft
    pr b 0 (y+sz) sz 1
    w w
  rn
rn

# Move rectangle from top left to bottom right
le

# Paint this top right of the display
pa f 60 1 5x5p h 1 1 glcdfillrectangle2
pa f 70 8 5x5p h 1 1 - top align -

# Repeat for each fill type (1..3)
rw ft < 4 1 1
  # Move from top left to bottom right
  rw x <= (h-sz+1) 0 1
    vs y x/(h-sz+1)*(v-sz+1)
    prf f x y sz sz al ft
    w w
    pr b x y 1 sz
    pr b x y sz 1
  rn
  prf f h-sz+1 v-sz+1 sz sz 0 5
rn

# Move rectangle from bottom left to top right
le

# Paint this bottom right of the display
pa f 60 50 5x5p h 1 1 glcdfillrectangle2
pa f 70 57 5x5p h 1 1 - top align -

# Repeat for each fill type (1..3)
rw ft < 4 1 1
  # Move from bottom left to top right
  rw x <= (h-sz+1) 0 1
    vs dy x/(h-sz+1)*(v-sz+1)
    prf f x v-sz+1-dy sz sz al ft
    w w
    pr b x v-sz+1-dy 1 sz
    pr b x v-dy sz 1
  rn
  prf f h-sz+1 0 sz sz 0 5
rn

# Closing down
w 100
le
w 100

