#
# Test command script for the Monochron emulator
#
# Purpose: This script is used for testing gldcdLine()
#          and numeric argument expressions
#

# Erase LCD display
le

# Set horizontal and vertical display size
vs hor=127
vs ver=63
# Set number of lines to draw minus 1
vs lines=6

# Paint in total 5x5 lines twice
rf factor=1 factor<=lines-1 factor=factor+1
  # From left to top and left to bottom
  pl f 0 ver/lines*factor (hor-(hor/lines*factor))/2 0
  pl f 0 ver/lines*factor (hor/lines*factor)/2 ver
  pl f 64 ver/lines*factor (hor-(hor/lines*factor))/2+64 0
  pl f 64 ver/lines*factor (hor/lines*factor)/2+64 ver

  # From right to top and right to bottom
  pl f hor/2 ver-(ver/lines*factor) (hor-(hor/lines*factor))/2 0
  pl f hor/2 ver-(ver/lines*factor) (hor/lines*factor)/2 ver
  pl f hor ver-(ver/lines*factor) (hor-(hor/lines*factor))/2+64 0
  pl f hor ver-(ver/lines*factor) (hor/lines*factor)/2+64 ver
rn

# Paint the glcdline function name in a rectangle box
pr f 17 27 31 9
pa f 19 29 5x5p h 1 1 glcdline
pr f 81 27 31 9
pa f 83 29 5x5p h 1 1 glcdline

